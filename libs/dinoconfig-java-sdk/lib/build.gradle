/*
 * DinoConfig Java SDK - Maven Central Publishing
 */

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.dinoconfig'
version = project.findProperty('sdkVersion') ?: '0.0.1-SNAPSHOT'
description = 'Official Java SDK for DinoConfig API'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation libs.junit.jupiter
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.1'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.16.1'
    implementation 'org.slf4j:slf4j-api:2.0.9'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
    withJavadocJar()
}

jar {
    manifest {
        attributes(
            'Implementation-Title': 'DinoConfig Java SDK',
            'Implementation-Version': version,
            'Implementation-Vendor': 'DinoConfig Team'
        )
    }
}

javadoc {
    options {
        encoding = 'UTF-8'
        addStringOption('Xdoclint:none', '-quiet')
    }
    failOnError = false
}

tasks.named('test') {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = 'dinoconfig-java-sdk'
            
            pom {
                name = 'DinoConfig Java SDK'
                description = 'Official Java SDK for DinoConfig API - Type-safe configuration management'
                url = 'https://developer.dinoconfig.com/docs/java-sdk'
                inceptionYear = '2025'
                
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                
                developers {
                    developer {
                        id = 'dinoconfig'
                        name = 'DinoConfig Team'
                        email = 'support@dinoconfig.com'
                    }
                }
                
                scm {
                    url = 'https://github.com/Dino-Config/dinoconfig'
                    connection = 'scm:git:git://github.com/Dino-Config/dinoconfig.git'
                    developerConnection = 'scm:git:ssh://git@github.com/Dino-Config/dinoconfig.git'
                }
            }
        }
    }
    
    repositories {
        maven {
            name = 'local'
            url = layout.buildDirectory.dir('repo')
        }
    }
}

signing {
    required { !version.toString().endsWith('SNAPSHOT') }
    sign publishing.publications.mavenJava
}

def bundleVersion = version.toString()
tasks.register('generateCentralBundle', Zip) {
    description = 'Generates bundle ZIP for Central Portal upload'
    group = 'publishing'
    dependsOn 'publishMavenJavaPublicationToLocalRepository'
    from layout.buildDirectory.dir('repo')
    archiveFileName = "dinoconfig-java-sdk-${bundleVersion}-bundle.zip"
    destinationDirectory = layout.buildDirectory.dir('central-bundle')
}
